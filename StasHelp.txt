https://www.2150692.ru/faq/62-gprs-svyaz-cherez-sim800l-i-arduino

GPRS связь с сервером через SIM800L и Arduino
 Создано: 28 августа 2015
arduino связь датчики GPRS
В текущей статье рассмотим как с помощью GSM модуля SIM800L и платы Arduino UNO R3 отправлять данные на Ваш сайт. Будем отправлять изменения состояния магнитного датчика (геркона), изменилось состояние датчика отправили сайт время и состояние датчика. Получится прототип сигнализации и ведением журнала на открывание/закрываение двери или окна. Для работы нам дополнительно понадобится наличие своего сайта с возможностью работы с PHP.

Постановка задачи
Мониторить состояние магнитоного датчика (геркона) и при изменении его состояния отправлять на сайт время и состояние датчика
На сайте сохранять принятые данные в файл журнала
Подготовка и подключение
Собираем устройство согласно схеме на картинке
Схема подключения SIM800L и датчика геркона к Arduino
Учитывая, что напряжение уровней модуля должны быть 2.8В, то следует сделать делители напряжений для контактов идущих от RX/TX модуля к 2,3-му на Ардуине. В моем случае работало без проблем и так.
Заливаем в Arduino скетч. Вместо сайта "mysite.ru" вписывайте свой адрес сайта
Создаем файл PHP скрипта и закачиваем его на сервер
Скетч (для клиента)
#include <SoftwareSerial.h>
SoftwareSerial GSMport(2, 3); // RX, TX
int char_;
int SensorPin = 10;
int SensorState;
int SensorLastState = HIGH;

void setup() {
  delay(3000); //дадим время на инициализацию GSM модулю
  pinMode(SensorPin, INPUT);
  digitalWrite(SensorPin, HIGH);  //вкл. подтягивающий резистор 20ом
  Serial.begin(9600);  //скорость порта
  Serial.println("GPRS test");
  GSMport.begin(9600);
  gprs_init();
}

void loop() {
  SensorState = digitalRead(SensorPin);
  if (SensorState != SensorLastState) {  //если смена состояния датчика
    Serial.print("sensor changed to: ");
    Serial.println(SensorState);
    SensorLastState = SensorState;
    gprs_send(String(SensorState));
    delay(100);
  }
  if (GSMport.available()) {  //если GSM модуль что-то послал нам, то
    Serial.println(ReadGSM());  //печатаем в монитор порта пришедшую строку
  }
  delay(100);
}

void gprs_init() {  //Процедура начальной инициализации GSM модуля
  int d = 500;
  int ATsCount = 7;
  String ATs[] = {  //массив АТ команд
    "AT+SAPBR=3,1,\"CONTYPE\",\"GPRS\"",  //Установка настроек подключения
    "AT+SAPBR=3,1,\"APN\",\"internet.tele2.ru\"",
    "AT+SAPBR=3,1,\"USER\",\"tele2\"",
    "AT+SAPBR=3,1,\"PWD\",\"tele2\"",
    "AT+SAPBR=1,1",  //Устанавливаем GPRS соединение
    "AT+HTTPINIT",  //Инициализация http сервиса
    "AT+HTTPPARA=\"CID\",1"  //Установка CID параметра для http сессии
  };
  int ATsDelays[] = {6, 1, 1, 1, 3, 3, 1}; //массив задержек
  Serial.println("GPRG init start");
  for (int i = 0; i < ATsCount; i++) {
    Serial.println(ATs[i]);  //посылаем в монитор порта
    GSMport.println(ATs[i]);  //посылаем в GSM модуль
    delay(d * ATsDelays[i]);
    Serial.println(ReadGSM());  //показываем ответ от GSM модуля
    delay(d);
  }
  Serial.println("GPRG init complete");
}

void gprs_send(String data) {  //Процедура отправки данных на сервер
  //отправка данных на сайт
  int d = 400;
  Serial.println("Send start");
  Serial.println("setup url");
  GSMport.println("AT+HTTPPARA=\"URL\",\"http://mysite.ru/?a=" + data + "\"");
  delay(d * 2);
  Serial.println(ReadGSM());
  delay(d);
  Serial.println("GET url");
  GSMport.println("AT+HTTPACTION=0");
  delay(d * 2);
  Serial.println(ReadGSM());
  delay(d);
  Serial.println("Send done");
}

String ReadGSM() {  //функция чтения данных от GSM модуля
  int c;
  String v;
  while (GSMport.available()) {  //сохраняем входную строку в переменную v
    c = GSMport.read();
    v += char(c);
    delay(10);
  }
  return v;
}
 

PHP скрипт (для сервера)
Сохраняем на сервере в файл index.php

<?php
  $ip = $_SERVER['REMOTE_ADDR']; //получаем IP адрес клиента
  $client = $_SERVER['HTTP_USER_AGENT']; //получаем идентификатор HTTP клиента
  $today = date("Y.m.d H:i:s"); //получаем текущие дату и время
  $f = fopen("log.csv","a"); //открываем файл для добавления данных
  $param = $_REQUEST['a']; //получаем значение посланной переменной "а"
  fwrite($f,"$today; $ip; $client; sensor=$param\r\n-----------------\r\n"); //запись данных в файл
  fclose($f); //закрываем файл
?>
<p>GPRS data read page</p>
 

Проверка
Состояние SIM800L при установленном GPRS соединении

Подавать питание рекомендую сперва GSM модулю, потом соединять Ардуину с ПК. Включаем монитор порта и наблюдаем.

Инициализация
Должны пробежать двойные строчки с АТ командами. Если показываются по одной АТ команде, значит нет связи с SIM800L. После инициализации при успешном создании GPRS сеанса светодиод на GSM модуле начнет часто моргать.

GPRS test
GPRG init start
AT+SAPBR=3,1,"CONTYPE","GPRS"
AT+SAPBR=3,1,"CONTYPE","GPRS"

OK

AT+SAPBR=3,1,"APN","internet.tele2.ru"
AT+SAPBR=3,1,"APN","internet.tele2.ru"

OK

AT+SAPBR=3,1,"USER","tele2"
AT+SAPBR=3,1,"USER","tele2"

OK

AT+SAPBR=3,1,"PWD","tele2"
AT+SAPBR=3,1,"PWD","tele2"

OK

AT+SAPBR=1,1
AT+SAPBR=1,1

OK

AT+HTTPINIT
AT+HTTPINIT

OK

AT+HTTPPARA="CID",1
AT+HTTPPARA="CID",1

OK

GPRG init complete
 

Сработка датчика и отправка на сервер
Отправка через открытый GET запрос с передачей параметра в URL строке. Значение состояния датчика передаем через переменную "а". "?a=0" - передана переменная "а" со значением "0".

sensor changed to: 0
Send start
setup url
AT+HTTPPARA="URL","http://mysite.ru/?a=0

ERROR

GET url
AT+HTTPACTION=0

OK

Send done

+HTTPACTION: 0,200,26
 

Проверяем сервер
На сервере должен появиться файл со следующим содержимым:

2015.09.02 13:26:51; 176.59.197.176; SIMCOM_MODULE; sensor=0
-----------------
2015.09.02 13:27:14; 176.59.199.8; SIMCOM_MODULE; sensor=1
-----------------
 

Теперь вы можете самостоятельно сделать свой GPS трекер на авто или метео станцию со сбором данных.

Иногда спрашивают, а как бы через интернет поуправлять Ардуиной с GSM модулем? Одним из решений считаю на сервере сделать скрипт, который бы при запросе от SIM800L возвращал нужные команды и далее можно их анализировать в коде скетча.

На этом все. Вопросы и пожелания пишите в комментариях.